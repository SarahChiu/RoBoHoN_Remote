apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 21
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.example.airobo.remotecontrol"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.10.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    provided files('jar/jp.co.sharp.android.voiceui.framework.jar')
    provided files('jar/jp.co.sharp.android.rb.projector.framework.jar')
    provided files('jar/jp.co.sharp.android.rb.addressbook.framework.jar')
    provided files('jar/jp.co.sharp.android.rb.cameralibrary.jar')
    provided files('jar/jp.co.sharp.android.rb.rbdance.framework.jar')

    compile fileTree(include: ['*.jar'], dir: 'libs')
    //grpc need
    compile 'io.grpc:grpc-okhttp:1.10.0'
    compile 'io.grpc:grpc-protobuf-lite:1.10.0'
    compile 'io.grpc:grpc-stub:1.10.0'

    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.guava:guava:18.0'
    compile 'javax.annotation:javax.annotation-api:1.2'

    testCompile "junit:junit:4.12"
}
